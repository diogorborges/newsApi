// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    ext.kotlin_version = '1.3.21'
    ext.gradle_version = '3.3.2'
    repositories {
        google()
        jcenter()
        
    }
    dependencies {
        classpath "com.android.tools.build:gradle:$gradle_version"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

allprojects {
    repositories {
        google()
        jcenter()
        
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

ext.config = [
        minSdkVersion    : 19,
        compileSdkVersion: 28,
        targetSdkVersion : 28
]

ext.versions = [
        appcompat       : '1.1.0-alpha03',
        material        : '1.1.0-alpha05',
        constraintLayout: '1.1.3',
        jUnit           : '4.12',
        testRunner      : '1.1.2-alpha02',
        espresso        : '3.1.0-alpha4',
        ktlint          : '0.24.0',
        ktx             : '1.1.0-alpha05',
        dagger          : '2.13'
]

subprojects {

    configurations {
        ktlint
    }

    dependencies {
        ktlint "com.github.shyiko:ktlint:${versions.ktlint}"
    }

    task ktlint(type: JavaExec, group: "verification") {
        description = "Check Kotlin code style."
        classpath = configurations.ktlint
        main = "com.github.shyiko.ktlint.Main"
        args "src/**/*.kt"
    }

    task ktlintFormat(type: JavaExec, group: "formatting") {
        description = "Fix Kotlin code style deviations."
        classpath = configurations.ktlint
        main = "com.github.shyiko.ktlint.Main"
        args "-F", "src/**/*.kt"
    }

    if (project.tasks.findByName('check')) {
        check.dependsOn ktlint
    }
}
